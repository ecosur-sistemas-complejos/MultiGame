<?xml version="1.0"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
        title="Manantiales de La Sierra" creationComplete="init(event)"
        skinClass="mx.ecosur.mobile.skins.BitmapSkin">
    <fx:Metadata >
        [Event(name="gameChosen", type="mx.ecosur.multigame.event.PlayEvent")]
        [ResourceBundle("ManantialesAir")]
    </fx:Metadata>

    <fx:Declarations>
        <s:RemoteObject id="gameService" destination="gameService"
            channelSet="{FlexGlobals.topLevelApplication.amfChannelSet}"
            result="resultHandler(event)"
            fault="faultHandler(event)" />
        <s:Consumer id="listener" destination="lobby-destination"
            channelSet="{FlexGlobals.topLevelApplication.amfChannelSet}"
            fault="messageFaultHandler(event)"
            message="messageResultHandler(event)"
            selector="NOTIFICATION_EVENT in ('CREATE','JOIN', 'DESTROY')"/>
        <s:ArrayCollection id="manModes">
            <fx:Object label="{resourceManager.getString('ManantialesAir','mode.competitive')}" data="COMPETITIVE" />
            <!--<fx:Object label="{resourceManager.getString('ManantialesAir','mode.puzzle')}" data="BASIC_PUZZLE" />-->
            <fx:Object label="{resourceManager.getString('ManantialesAir','mode.silvopastoral')}" data="SILVOPASTORAL"/>
            <!--<fx:Object label="{resourceManager.getString('ManantialesAir','mode.silvo.puzzle'}" data="SILVO_PUZZLE"/>-->
        </s:ArrayCollection>
        <s:ArrayList id="strategies">
            <fx:String>SIMPLE</fx:String>
            <fx:String>SIMPLE</fx:String>
            <fx:String>SIMPLE</fx:String>
        </s:ArrayList>
    </fx:Declarations>

    <fx:Script>
        <![CDATA[
        import mx.collections.ArrayCollection;
        import mx.core.FlexGlobals;
        import mx.ecosur.mobile.component.Alert;
        import mx.ecosur.multigame.dao.GameDAO;
        import mx.ecosur.multigame.enum.Color;
        import mx.ecosur.multigame.enum.GameState;
        import mx.ecosur.multigame.enum.NotificationEvent;
        import mx.ecosur.multigame.manantiales.entity.ManantialesGame;
        import mx.ecosur.multigame.manantiales.entity.SimpleAgent;

        import mx.messaging.events.MessageEvent;
        import mx.messaging.events.MessageFaultEvent;
        import mx.messaging.messages.IMessage;
        import mx.rpc.AsyncToken;

        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;

        import spark.formatters.DateTimeFormatter;

        protected var modalities:ArrayCollection = new ArrayCollection();

        private var _games:ArrayCollection = new ArrayCollection();

        private var _modes:ArrayCollection = new ArrayCollection();

        private var unusedGame:ManantialesGame;

        private var unusedAgent:SimpleAgent;

        public function alert(msg:String):void {
            var a:Alert = new Alert();
            a.open(this, true);
            a.alertmsg.text = msg;
            var metrics:TextLineMetrics = a.measureText(msg);
            a.x = (this.width - metrics.width) / 2;
            a.y = (this.height - metrics.height) / 3;
        }

        protected function init(event:Event):void {
            /* Load save and pending games */
            gameService.getUnfinishedGames(FlexGlobals.topLevelApplication.registrant);
            gameService.getPendingGames(FlexGlobals.topLevelApplication.registrant);

            /* subscribe the listener */
            listener.subscribe();

            /* add swipe handler to data grid */
            Multitouch.inputMode = MultitouchInputMode.GESTURE;
            gameGrid.addEventListener (TransformGestureEvent.GESTURE_SWIPE, swipeGame);
        }

        protected function createGame(event:Event):void {
            GameView.gameId = 0;
            /* Only one type of Manantiales agent, so choice is not displayed in mobile client */
            if (ai.selected) {
                gameService.startNewGameWithAI(FlexGlobals.topLevelApplication.registrant, Color.UNKNOWN, "Manantiales", modeList.selectedItem.data, strategies);
            } else {
                gameService.startNewGame(FlexGlobals.topLevelApplication.registrant, Color.UNKNOWN, "Manantiales", modeList.selectedItem.data);
            }
        }

        protected function resultHandler(event:ResultEvent):void {
            if (event.result == null)
                return;
            var response:Array = Array(event.result);

            for (var i:int = 0; i < response.length; i++) {
                if (response[i] is ArrayCollection) {
                    var games:ArrayCollection = ArrayCollection(response[ i ]);
                    for (var j:int = 0; j < games.length; j++) {
                        var d:GameDAO = GameDAO(games [ j ]);
                        if (d.gameType != "Gente") {
                            _games.addItem(d);
                        }

                    }
                    updateGrid();
                } else if (response [ i ] is GameDAO) {
                    var dao:GameDAO = GameDAO(response [ i ]);
                    var added:Boolean = false;
                    for (var j:int = 0; j < _games.length; j++) {
                        var d:GameDAO = GameDAO(_games [ j ]);
                        if (d.gameId == dao.gameId) {
                            d = dao;
                            added = true;
                            break;
                        }
                    }
                    if (!added)
                        _games.addItem(dao);
                    updateGrid();
                } else if (response [ i ] is ManantialesGame) {
                    var g:ManantialesGame = ManantialesGame(response [ i ]);
                    GameView.gameId = g.id;
                    navigator.pushView(GameView);
                }
            }
        }

        protected function faultHandler(event:FaultEvent):void {
            trace("{resourceManager.getString('ManantialesAir','fault.message')} [" + event.fault.message + "]");
        }

        protected function messageFaultHandler(event:MessageFaultEvent):void {
            trace("{resoureceManager.getString('ManantialesAir','fault.message.fault')} [" + event.faultString);
        }

        protected function messageResultHandler(event:MessageEvent):void {
            var message:IMessage = IMessage(event.message);
            trace("Message: " + message);
            var dao:GameDAO = GameDAO(message.body);
            switch (message.headers.NOTIFICATION_EVENT) {
                case NotificationEvent.CREATE:
                    trace("Creating DAO: " + dao);
                    if (dao.gameType != "Gente") {
                        _games.addItem(dao);
                    }
                    break;
                case NotificationEvent.DESTROY:
                    trace("Destroying DAO: " + dao);
                    if (dao.gameType != "Gente") {
                        var idx:int = findIndex(dao);
                        trace("Index to be removed: " + idx);
                        if (idx > 0)
                            _games.removeItemAt(idx);
                    }
                    break;
                case NotificationEvent.JOIN:
                    trace("Join event for DAO: " + dao);
                    if (dao.gameType != "Gente") {
                        var idx:int = findIndex(dao);
                        trace("Index to be updated: " + idx);
                        if (idx > 0) {
                            _games.removeItemAt(idx);
                            _games.addItemAt(dao, idx);
                        }
                    }
                    break;
                default:
                    break;
            }

            /* always update the grid for any changes */
            updateGrid();
        }

        protected function joinGame(event:MouseEvent):void {
            var tok:AsyncToken;

            var idx:int = gameGrid.selectedIndex;
            var g:GameDAO = GameDAO(_games.getItemAt(idx));

            if (!containsPlayer(g)) {
                gameService.joinPendingGame(g.gameId, FlexGlobals.topLevelApplication.registrant, Color.UNKNOWN);
            }

            GameView.gameId = g.gameId;
            navigator.pushView(GameView);
        }

        protected function swipeGame(event:TransformGestureEvent):void {
            trace(event);
            this.alert("Swiped! [" + event.toString() + ")");

        }

        private function updateGrid():void {
            var dataSource:ArrayCollection = new ArrayCollection();
            for (var i:int = 0; i < _games.length; i++) {
                var dao:GameDAO = GameDAO(_games [ i ]);
                dataSource.addItem(model(dao));
            }
            gameGrid.dataProvider = dataSource;
        }

        private function containsPlayer(dao:GameDAO):Boolean {
            var ret:Boolean = false;
            for (var i:int = 0; i < dao.players.length; i++) {
                if (dao.players[i] == FlexGlobals.topLevelApplication.registrant.name) {
                    ret = true;
                    break;
                }
            }

            return ret;
        }

        private function model(dao:GameDAO):Object {
            var dataItem:Object = new Object();
            dataItem.gameType = dao.gameType;
            var df:DateTimeFormatter = new DateTimeFormatter();
            df.dateTimePattern = resourceManager.getString("ManantialesAir", "date.format");
            dataItem.created = df.format (dao.creationDate);
            var p:String = "";
            for (var i:Number = 0; dao.players != null && i < dao.players.length; i++) {
                p += dao.players[i] + ", ";
            }
            p = p.substring(0, p.length - 2);
            dataItem.players = p;
            dataItem.status = GameState.getDescription(dao.status);
            return dataItem;
        }

        private function Logout(event:Event):void {
            FlexGlobals.topLevelApplication.amfChannelSet.logout();
            navigator.popToFirstView();
        }

        override public function createReturnObject():Object {
            return LoginView.remoteServer;
        }

        protected function findIndex(dao:GameDAO):int {
            var idx:int = -1;
            for (var i:int = 0; i < _games.length; i++) {
                var ck:GameDAO = GameDAO(_games [ i ]);
                if (ck.gameId = dao.gameId) {
                    idx = i;
                    break;
                }
            }


            return idx;

        }
        ]]>
    </fx:Script>

    <s:actionContent>
        <s:Button label="{resourceManager.getString('ManantialesAir','login.configuration')}" buttonDown="{navigator.pushView(ConfigurationView)}"/>
    </s:actionContent>

    <s:Group id="boxes">
        <s:layout>
            <s:BasicLayout/>
        </s:layout>
        <s:Group id="games" horizontalCenter="0" height="200">
            <s:List id="gameGrid" width="1004" height="100%" doubleClickEnabled="true" doubleClick="joinGame(event)"
                    itemRenderer="mx.ecosur.mobile.renderers.GameListRenderer" alpha="0.5" />
        </s:Group>
        <s:Group id="settings" horizontalCenter="0" verticalCenter="180">
            <s:layout>
                <s:HorizontalLayout/>
            </s:layout>
            <s:SpinnerListContainer >
                <s:SpinnerList id="modeList" dataProvider="{manModes}" width="100"/>
            </s:SpinnerListContainer>
            <s:RadioButton groupName="robots" id="human" label="{resourceManager.getString('ManantialesAir','lobby.human')}" selected="true"/>
            <s:RadioButton groupName="robots" id="ai" label="{resourceManager.getString('ManantialesAir','lobby.robots')}"/>
            <s:Button label="{resourceManager.getString('ManantialesAir','lobby.newgame')}" id="start" buttonDown="{createGame(event)}"/>
        </s:Group>
    </s:Group>
</s:View>
